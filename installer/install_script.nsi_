!include "MUI2.nsh"
!include "FileAssociation.nsh"

Name "KBE"

; set the name of the installer
OutFile "kbe-win32-0.3.0.exe"

;Default installation folder
InstallDir $PROGRAMFILES32\KBE

;Get installation folder from registry if available
InstallDirRegKey HKCU "Software\KBE" ""
;Request application privileges for Windows Vista
RequestExecutionLevel admin

;--------------------------------
;Interface Settings

  !define MUI_ABORTWARNING

;--------------------------------
;Pages

  !insertmacro MUI_PAGE_LICENSE "sources\COPYING"
  !insertmacro MUI_PAGE_COMPONENTS
  !insertmacro MUI_PAGE_DIRECTORY
  
  ;Start Menu Folder Page Configuration
  !define MUI_STARTMENUPAGE_REGISTRY_ROOT "HKCU"
  !define MUI_STARTMENUPAGE_REGISTRY_KEY "Software\KBE"
  !define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "KBE"
  var StartMenuFolder
  !insertmacro MUI_PAGE_STARTMENU Application $StartMenuFolder
  
  !insertmacro MUI_PAGE_INSTFILES
  
  !insertmacro MUI_UNPAGE_CONFIRM
  !insertmacro MUI_UNPAGE_INSTFILES
;Languages
 
	!insertmacro MUI_LANGUAGE "English"
	!insertmacro MUI_LANGUAGE "Russian"

;--------------------------------

;Installation types
	InstType "Full"
	InstType "Minimal"
	
;--------------------------------
# create an install section.
Section ;"Editor"
 
%INST_FILES%

;Store installation folder
  WriteRegStr HKCU "Software\KBE" "" $INSTDIR
  
  ;Create uninstaller
  WriteUninstaller "$INSTDIR\Uninstall.exe"
  
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
    
    ;Create shortcuts
CreateDirectory "$SMPROGRAMS\$StartMenuFolder"
CreateShortCut "$SMPROGRAMS\$StartMenuFolder\KBE.lnk" "$INSTDIR\kbe.exe"
CreateShortCut "$SMPROGRAMS\$StartMenuFolder\User Manual.lnk" "$INSTDIR\manual\User Guide.pdf"
CreateShortCut "$SMPROGRAMS\$StartMenuFolder\Uninstall.lnk" "$INSTDIR\Uninstall.exe"

!insertmacro MUI_STARTMENU_WRITE_END

SectionEnd

Section "Uninstall"
Delete "$INSTDIR\Uninstall.exe"

%UNINST_FILES%

%UNINST_EXAMPLE%

  Delete "$INSTDIR\plugins\m4scp\libm4scp.a"
  Delete "$INSTDIR\plugins\m4scp\m4scp.dll"
  RMDir "$INSTDIR\plugins\m4scp"
  
  Delete "$INSTDIR\plugins\scg\libscg.a"
  Delete "$INSTDIR\plugins\scg\scg.dll"
  RMDir "$INSTDIR\plugins\scg"

  Delete "$INSTDIR\plugins\scs\libscs.a"
  Delete "$INSTDIR\plugins\scs\scs.dll"
  RMDir "$INSTDIR\plugins\scs"
  
  RMDir "$INSTDIR\plugins"

  Delete "$INSTDIR\manual\User Guide.pdf"
  RMDir "$INSTDIR\manual"
  
  !insertmacro MUI_STARTMENU_GETFOLDER Application $StartMenuFolder
    
  Delete "$SMPROGRAMS\$StartMenuFolder\examples.lnk"
  Delete "$SMPROGRAMS\$StartMenuFolder\KBE.lnk"
  Delete "$SMPROGRAMS\$StartMenuFolder\Uninstall.lnk"
  RMDir "$SMPROGRAMS\$StartMenuFolder"

  RMDir "$INSTDIR"
  DeleteRegKey /ifempty HKCU "Software\KBE"
  
  ${unregisterExtension} ".gwf" "GWF File"
  ${unregisterExtension} ".m4scp" "M4SCP File"
  ${unregisterExtension} ".scs" "SCs File"
SectionEnd

Section "Examples" example
	SectionIn 1
	%INST_EXAMPLE%
	
	CreateShortCut "$SMPROGRAMS\$StartMenuFolder\examples.lnk" "$INSTDIR\example"
SectionEnd

SectionGroup "Plugins" plugins
	Section "M4SCP" m4scp
		SectionIn 1
		SetOutPath "$INSTDIR\plugins\m4scp"
		File "sources\plugins\m4scp.dll"
		File "sources\plugins\libm4scp.a"
		
		${registerExtension} "$INSTDIR\kbe.exe" ".m4scp" "M4SCP File"
	SectionEnd

	Section "SCg" scg
		SectionIn 1 2
		SetOutPath "$INSTDIR\plugins\scg"
		File "sources\plugins\scg.dll"
		File "sources\plugins\libscg.a"
		
		${registerExtension} "$INSTDIR\kbe.exe" ".gwf" "GWF File"
	SectionEnd

	Section "SCs" scs
		SectionIn 1
		SetOutPath "$INSTDIR\plugins\scs"
		File "sources\plugins\scs.dll"
		File "sources\plugins\libscs.a"
		
		${registerExtension} "$INSTDIR\kbe.exe" ".scs" "SCs File"
	SectionEnd
	
SectionGroupEnd

Section "Documentation" doc
	SectionIn 1 2
	SetOutPath "$INSTDIR\manual"
	File "sources\manual\User Guide.pdf"
SectionEnd

Function .onInit
	!insertmacro MUI_LANGDLL_DISPLAY
FunctionEnd

LangString DESC_Section1 ${LANG_ENGLISH} "Contains some samples written in SCg, M4SCP and SCs"
LangString DESC_Section1 ${LANG_RUSSIAN} "Содержит несколько примеров, записанных на SCg, M4SCP и SCs"
LangString DESC_Section2 ${LANG_ENGLISH} "Standard editor's plugins"
LangString DESC_Section2 ${LANG_RUSSIAN} "Плагины для редактора из стандартной поставки"
LangString DESC_Section3 ${LANG_ENGLISH} "Plugin for editting scp-program in m4scp format"
LangString DESC_Section3 ${LANG_RUSSIAN} "Плагин для редактирования scp-программ в формате m4scp"
LangString DESC_Section4 ${LANG_ENGLISH} "Plugin for editting simple gwf files"
LangString DESC_Section4 ${LANG_RUSSIAN} "Плагин для редактирования gwf файлов"
LangString DESC_Section5 ${LANG_ENGLISH} "Plugin for editting linear records of SC"
LangString DESC_Section5 ${LANG_RUSSIAN} "Плагин для редактирования линейных записей SC"
LangString DESC_Section6 ${LANG_ENGLISH} "Reference documentation"
LangString DESC_Section6 ${LANG_RUSSIAN} "Справочная информация"

!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${example} $(DESC_Section1)
  !insertmacro MUI_DESCRIPTION_TEXT ${plugins} $(DESC_Section2)
  !insertmacro MUI_DESCRIPTION_TEXT ${m4scp} $(DESC_Section3)
  !insertmacro MUI_DESCRIPTION_TEXT ${scg} $(DESC_Section4)
  !insertmacro MUI_DESCRIPTION_TEXT ${scs} $(DESC_Section5)
  !insertmacro MUI_DESCRIPTION_TEXT ${doc} $(DESC_Section6)
!insertmacro MUI_FUNCTION_DESCRIPTION_END

